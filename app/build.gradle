apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.kiwiandroiddev.sc2buildassistant"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 260
        versionName "2.6.0"
        setProperty("archivesBaseName", "kiwiandroiddev-sc2buildassistant-$versionName")

        vectorDrawables.useSupportLibrary = true

        testApplicationId "com.kiwiandroiddev.sc2buildassistant.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    signingConfigs {
        release {
            def Properties keyProps = new Properties()
            keyProps.load(new FileInputStream(file('../keystore.properties')))
            storeFile file(keyProps["store"])
            keyAlias keyProps["alias"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        disable 'InvalidPackage'

        // Continue when errors are found in release builds (e.g. translation issues)
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

dependencies {
    compile "com.android.support:support-v4:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:design:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${rootProject.ext.supportLibVersion}"
    compile("com.afollestad.material-dialogs:core:${rootProject.ext.materialDialogsVersion}@aar") {
        transitive = true
    }
    // Contains MaterialSimpleListAdapter
    compile("com.afollestad.material-dialogs:commons:${rootProject.ext.materialDialogsVersion}@aar") {
        transitive = true
    }
    compile files('libs/libGoogleAnalyticsServices.jar')
    compile "com.google.firebase:firebase-ads:${rootProject.ext.firebaseVersion}"
    compile "com.google.firebase:firebase-crash:${rootProject.ext.firebaseVersion}"

    compile(
            'com.google.code.gson:gson:2.4',
            'io.reactivex:rxandroid:0.25.0',
            'com.jakewharton:butterknife:6.0.0',
            'com.f2prateek.dart:dart:1.1.0',
            'com.jakewharton.timber:timber:3.0.2',
            'com.karumi:dexter:2.2.2'
    )

    testCompile(
            'junit:junit:4.12',
            'org.mockito:mockito-core:1.9.5',
            'org.assertj:assertj-core:1.7.0'
    )
}

apply plugin: 'com.google.gms.google-services'