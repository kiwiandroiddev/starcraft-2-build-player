apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.kiwiandroiddev.sc2buildassistant"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 250
        versionName "2.5.0"

        testApplicationId "com.kiwiandroiddev.sc2buildassistant.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    signingConfigs {
        release {
            def Properties keyProps = new Properties()
            keyProps.load(new FileInputStream(file('../keystore.properties')))
            storeFile file(keyProps["store"])
            keyAlias keyProps["alias"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        disable 'InvalidPackage'

        // Continue when errors are found in release builds (e.g. translation issues)
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

dependencies {
    compile "com.android.support:support-v4:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:design:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${rootProject.ext.supportLibVersion}"
    compile 'com.google.code.gson:gson:2.4'
    compile 'io.reactivex:rxandroid:0.25.0'
    compile 'com.jakewharton:butterknife:6.0.0'
    compile 'com.f2prateek.dart:dart:1.1.0'
    compile 'com.jakewharton.timber:timber:3.0.2'
    compile 'com.karumi:dexter:2.2.2'
    compile('com.afollestad.material-dialogs:core:0.8.4.2@aar') {
        transitive = true
    }
    // Contains MaterialSimpleListAdapter
    compile('com.afollestad.material-dialogs:commons:0.8.4.2@aar') {
        transitive = true
    }
    compile files('libs/GoogleAdMobAdsSdk-6.2.1.jar')
    compile files('libs/libGoogleAnalyticsServices.jar')

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.assertj:assertj-core:1.7.0'
}
