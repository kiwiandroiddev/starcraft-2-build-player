apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.jakewharton.hugo'

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/jetbrains/spek" }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.kiwiandroiddev.sc2buildassistant"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 270
        versionName "2.7.0"
        setProperty("archivesBaseName", "kiwiandroiddev-sc2buildassistant-$versionName")

        vectorDrawables.useSupportLibrary = true

        testApplicationId "com.kiwiandroiddev.sc2buildassistant.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            Properties keyProps = new Properties()
            keyProps.load(new FileInputStream(file('../keystore.properties')))
            storeFile file(keyProps["store"])
            keyAlias keyProps["alias"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        disable 'InvalidPackage'

        // Continue when errors are found in release builds (e.g. translation issues)
        abortOnError false

        lintConfig file('lint.xml')
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

kapt {
    generateStubs = true
}

dependencies {
    compile "com.android.support:support-v4:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"
    compile "com.android.support:recyclerview-v7:$supportLibVersion"
    compile("com.afollestad.material-dialogs:core:$materialDialogsVersion@aar") {
        transitive = true
    }
    // Contains MaterialSimpleListAdapter
    compile("com.afollestad.material-dialogs:commons:$materialDialogsVersion@aar") {
        transitive = true
    }
    compile files('libs/libGoogleAnalyticsServices.jar')
    compile "com.google.firebase:firebase-ads:$firebaseVersion"
    compile "com.google.firebase:firebase-crash:$firebaseVersion"

    compile(
            "com.google.dagger:dagger:$dagger2_version",
            "com.google.code.gson:gson:$gson_version",
            "io.reactivex.rxjava2:rxjava:$rxjava_version",
            "io.reactivex.rxjava2:rxandroid:$rxandroid_version",
            'com.jakewharton.rxrelay2:rxrelay:2.0.0',
            'com.jakewharton.rxbinding2:rxbinding:2.0.0',
            'com.jakewharton:butterknife:8.6.0',
            'com.jakewharton.timber:timber:3.0.2',
            "com.squareup.retrofit2:retrofit:$retrofit_version",
            "com.squareup.retrofit2:converter-gson:$retrofit_version",
            "com.squareup.retrofit2:converter-scalars:$retrofit_version",
            "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version",
            "com.squareup.okhttp3:okhttp:$okhttp_version",
            "com.squareup.okhttp3:logging-interceptor:$okhttp_version",
            'com.karumi:dexter:2.2.2'
    )

    compile "android.arch.lifecycle:runtime:$arch_version"
    compile "android.arch.lifecycle:extensions:$arch_version"
    annotationProcessor "android.arch.lifecycle:compiler:$arch_version"

    kapt 'com.jakewharton:butterknife-compiler:8.6.0'

    provided "org.glassfish:javax.annotation:$java_annotation_version"
    kapt "com.google.dagger:dagger-compiler:$dagger2_version"

    testCompile(
            'junit:junit:4.12',
            'org.mockito:mockito-core:1.9.5',
            'org.assertj:assertj-core:1.7.0'
    )

    // Spek
    testCompile("org.jetbrains.spek:spek-api:$spek_version") {
        exclude group: 'org.jetbrains.kotlin'
    }
    testCompile("org.junit.platform:junit-platform-runner:1.0.0-M4")
    testCompile("org.jetbrains.spek:spek-junit-platform-engine:$spek_version") {
        exclude group: 'org.jetbrains.kotlin'
    }
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.0-M4'
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Mockito-Kotlin
    // Workaround to have mockito-kotlin work with kotlin 1.1 until the library author upgrades
    testCompile("com.nhaarman:mockito-kotlin:1.4.0") {
        exclude group: 'org.jetbrains.kotlin'
    }

    // workaround for "Error:Conflict with dependency 'com.google.code.findbugs:jsr305' in
    // project ':app'. Resolved versions for app (3.0.1) and test app (2.0.1) differ.
    // See http://g.co/androidstudio/app-test-app-conflict for details."
    android {
        configurations.all {
            resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
        }
    }

    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestCompile 'tools.fastlane:screengrab:1.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}

apply plugin: 'com.google.gms.google-services'
